extends Node

signal noray_connected
signal show_wait_screen


const NORAY_ADDRESS = "tomfol.io"
const NORAY_PORT = 8890

var is_host = false
var external_oid = ""


func _ready() -> void:
	Noray.on_connect_to_host.connect(on_noray_connected)
	Noray.on_connect_nat.connect(handle_nat_connection)
	Noray.on_connect_relay.connect(handle_relay_connection)
	
	Noray.connect_to_host(NORAY_ADDRESS, NORAY_PORT)


func on_noray_connected():
	print("Connected to Noray server")
	
	Noray.register_host()
	await Noray.on_pid
	await Noray.register_remote()
	noray_connected.emit()

func host():
	print("Hosting")
	show_wait_screen.emit()
	var peer = ENetMultiplayerPeer.new()
	peer.create_server(Noray.local_port)
	print("Server started on port", Noray.local_port)
	multiplayer.multiplayer_peer = peer
	is_host = true

func join(oid):
	show_wait_screen.emit()
	Noray.connect_nat(oid)
	external_oid = oid

func handle_nat_connection(address, port):
	var err = await connect_to_server(address, port)
	
	if err != OK && !is_host:
		print("NAT failed, using relay")
		Noray.connect_relay(external_oid)
		err = OK
	
	return err

func handle_relay_connection(address, port):
	return await connect_to_server(address, port)


func connect_to_server(address, port):
	var err = OK
	
	if !is_host:
		var udp = PacketPeerUDP.new()
		udp.bind(Noray.local_port)
		udp.set_dest_address(address, port)
		
		print("Trying handshake to ", address, ":", port)
		err = await PacketHandshake.over_packet_peer(udp)
		print("Handshake result: ", err)
		udp.close()
		
		if err != OK:
			if err != ERR_BUSY:
				print("Handshake failed")
				return err
		else:
			print("Handshake success")
		
		var peer = ENetMultiplayerPeer.new()
		err = peer.create_client(address, port, 0, 0, 0, Noray.local_port)
		
		if err != OK:
			return err
		
		multiplayer.multiplayer_peer = peer
		
		return OK
	else:
		err = await PacketHandshake.over_enet(multiplayer.multiplayer_peer.host, address, port)
	
	return err
	
func reset():
	is_host = false
	external_oid = ""

	if multiplayer.multiplayer_peer:
		multiplayer.multiplayer_peer.close()
		multiplayer.multiplayer_peer = null

	await Noray.register_host()
	await Noray.on_pid
	await Noray.register_remote()
	noray_connected.emit()
